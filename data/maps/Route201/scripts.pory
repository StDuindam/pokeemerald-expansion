mapscripts Route201_MapScripts {
    MAP_SCRIPT_ON_TRANSITION: EventScript_Route201_FirstVisit
}

script EventScript_Route201_BoatAttendant{
    lock
    faceplayer
    msgbox(format("Hello champ, the boat goes whenever you want."))
    waitmessage
    msgbox(format("Would you like to go back to Slateport City?"),MSGBOX_YESNO)
    if(var(VAR_RESULT) == YES){
        msgbox(format("Wonderful, please board"))
        waitmessage
        warpdoor(MAP_SLATEPORT_CITY_HARBOR,4)
    }else{
        msgbox(format("Another time maybe."))
        waitmessage
    }
    release
}

script EventScript_Route201_FirstVisit{
    if(var(VAR_ROCKET_ARC_STATE) == 1)
    {
        setvar(VAR_ROCKET_ARC_STATE,2)
    }
}

script Route201_ProfessorHouse_TriggerOne{
    call(EventScript_Route201_YouAreHere)
    applymovement(OBJ_EVENT_ID_PLAYER, MoveToProfessor_201_One)
    waitmovement(0)
    call(EventScript_Route201_ProfessorAndSteven)
}

script Route201_ProfessorHouse_TriggerTwo{
    call(EventScript_Route201_YouAreHere)
    applymovement(OBJ_EVENT_ID_PLAYER, MoveToProfessor_201_Two)
    waitmovement(0)
    call(EventScript_Route201_ProfessorAndSteven)
}

script Route201_ProfessorHouse_TriggerThree{
    call(EventScript_Route201_YouAreHere)   
    applymovement(OBJ_EVENT_ID_PLAYER, MoveToProfessor_201_Three)
    waitmovement(0)
    call(EventScript_Route201_ProfessorAndSteven)
}

script Route201_ProfessorHouse_TriggerFour{
    call(EventScript_Route201_YouAreHere)   
    applymovement(OBJ_EVENT_ID_PLAYER, MoveToProfessor_201_Four)
    waitmovement(0)
    call(EventScript_Route201_ProfessorAndSteven)
}

script EventScript_Route201_YouAreHere{
    lock
    msgbox("Prof BIRCH: Ah you are here!")
    waitmessage
    release
}

script EventScript_Route201_ProfessorAndSteven{
    lock
    if(var(VAR_ROCKET_ARC_STATE) == 2){
        msgbox(format("Quite a nice little place we have created so far, haven't we? Anyway, the truth is beyond this beach are some incredibly strong pokemon. We do need your help to better understand this place and create a map of the Island. I have asked some of the Hoenn gym leaders to assist in this mission, like Steven here."))
        waitmessage
        applymovement(OBJ_EVENT_ID_PLAYER, PlayerFaceSteven_201)
        applymovement(2,StevenMoveToPlayer_Route201)
        waitmovement(0)
        msgbox(format("STEVEN: Right, I have agreed to assist scouting the island. So far the cries we have heard sound like ancient Pokemon. We have been hearing more and more sounds, something seems wrong. Make sure you keep your eyes open, okay? I will see you around and let the Professor know if you find something."))
        waitmessage
        applymovement(2, StevenLeaveHouse_Route201)
        waitmovement(0)
        removeobject(2)
        applymovement(OBJ_EVENT_ID_PLAYER, PlayerFaceBirch_201)
        msgbox(format("Prof BIRCH: Yes, yes, always keep an eye out for strange things, will you? With that in mind I think we are ready to venture into the forest. Find a strong Pokemon and return back to share your findings."))        
        waitmessage
        setvar(VAR_ROCKET_ARC_STATE,3)
        setflag(FLAG_ROUTE201_HIKER_BLOCKAGE)
    }else{
        faceplayer
        msgbox(format("Prof BIRCH: What exciting times, go see if you can catch a strong new Pokemon, make sure you come back after."))
        waitmessage
    }
    release
}

movement MoveToProfessor_201_One {
    walk_up * 3
}

movement MoveToProfessor_201_Two {
    walk_left
    walk_up * 2
}

movement MoveToProfessor_201_Three {
    walk_up
    walk_left * 2
    walk_up
}

movement PlayerFaceSteven_201{
    face_right
}

movement PlayerFaceBirch_201{
    face_up
}


movement MoveToProfessor_201_Four {
    walk_up * 2
    walk_left *3
    walk_up
}

movement  StevenMoveToPlayer_Route201 {
    walk_down
    walk_left * 5
    walk_up * 2
    face_left
}

movement  StevenLeaveHouse_Route201 {
    walk_down * 3
}

movement PlayerLookAtSteven_Route201{
    face_right
}

movement PlayerLookAtProfessor_Route201{
    face_up
}

script EventScript_Route201_Steven{
    lock
    faceplayer
    msgbox(format("STEVEN: You should go speak with the Professor, he will explain our next steps."))
    waitmessage
    release
}

script EventScript_Route201_Azuril{
    lock
    faceplayer
    msgbox(format("Azu! Azu!"))
    waitmessage
    release
}

script EventScript_Route201_Azumaril{
    lock
    faceplayer
    msgbox(format("Azumaril... Maril!"))
    waitmessage
    release
}

script EventScript_Route201_Scientist_One{
    lock
    faceplayer
    msgbox(format("Amazing, there are hardly any documents that speak of this island."))
    waitmessage
    release
}

script EventScript_Route201_Hiker_One{
    lock
    faceplayer
    msgbox(format("I can't wait to learn more about this place, maybe even hike up the mountain!"))
    waitmessage
    release
}

script EventScript_Route201_Hiker_Two{
    lock
    faceplayer
    msgbox(format("Sorry kid, I cannot let you pass yet. We are making sure it is safe out here. Try and find the professor."))
    waitmessage
    release
}

script EventScript_Route201_Pokeball_One{
    lock
    giveitem(ITEM_LEVEL_BALL)
    setflag(FLAG_ROUTE201_POKEBALL)
    removeobject(3)
    release
}

script SignScript_Route201_DiscoverySignpost{
    lock
    msgbox(format("Archeological Cleanery. Where finds get cleaned and identified."))
    waitmessage
    release
}

script EventScript_Route201_Scientist_Two{
    lock
    faceplayer
    msgbox(format("No no no... the tests come back negative, what is going on?"))
    waitmessage
    release
}

script EventScript_Route201_Scientist_Three{
    lock
    faceplayer
    msgbox(format("We are currently working on a full map of this island, or... well... no one has dared enter the area yet. Any day now though!"))
    waitmessage
    release
}

script EventScript_Route201_Hiker_Three{
    lock
    faceplayer
    msgbox(format("I have heard some loud noises coming from the forest-y area. This beach seems okay, but I won't go further myself."))
    waitmessage
    release
}

script EventScript_Route201_Mats_One{
    lock
    faceplayer
    msgbox(format("How much longer will we have to wait before we can leave this beach? I have been hearing rumours about a legendary fire pokemon. Take a look at this drawing."))
    showmonpic(SPECIES_MOLTRES,10,3)
    waitmessage
    msgbox(format("Wicked right? Maybe we will be lucky enough to see it ourselves."))
    waitmessage
    hidemonpic
    release
}

script EventScript_Route201_Jens_One{
    lock
    faceplayer
    msgbox(format("Whatever is hiding in that forest I'm sure we can beat it, right?"))
    waitmessage
    release
}