mapscripts RocketHideout_B1_MapScripts {
    MAP_SCRIPT_ON_TRANSITION: EventScript_OnLoad_RocketHideout_B1
}


script EventScript_OnLoad_RocketHideout_B1{
    if(flag(FLAG_ROCKETB1_BASEMENT_GAUNTLET_BEAT)){
        settrainerflag(TRAINER_ROCKETM_ROCKETHIDEOUTB1_ONE)
        settrainerflag(TRAINER_ROCKETM_ROCKETHIDEOUTB1_TWO)
        settrainerflag(TRAINER_ROCKETM_ROCKETHIDEOUTB1_THREE)
    }else{
        //cleartrainerflag()
        cleartrainerflag(TRAINER_ROCKETM_ROCKETHIDEOUTB1_FOUR)
        cleartrainerflag(TRAINER_ROCKETM_ROCKETHIDEOUTB1_FIVE)
        cleartrainerflag(TRAINER_ROCKETM_ROCKETHIDEOUTB1_SIX)
    }
    if(var(VAR_MEWTWO_ARC_STATE)==1){
        clearflag(FLAG_NOTINMEWTWO_STATE_1)
    }else{
    setflag(FLAG_NOTINMEWTWO_STATE_1)
    }
}

script EventScript_RocketHideout_B1_TriggerA{
    lock
    applymovement(OBJ_EVENT_ID_PLAYER, MovementRocketHideout_B1_A)
    waitmovement(0)
    call(EventScript_RocketHideout_B1_MovePlayer)
    release
}

movement MovementRocketHideout_B1_A {
    walk_right
    walk_down
    face_right
}

script EventScript_RocketHideout_B1_TriggerB{
    lock
    applymovement(OBJ_EVENT_ID_PLAYER, MovementRocketHideout_B1_B)
    waitmovement(0)
    call(EventScript_RocketHideout_B1_MovePlayer)
    release
}

movement MovementRocketHideout_B1_B {
    walk_right
}

script EventScript_RocketHideout_B1_TriggerC{
    lock
    applymovement(OBJ_EVENT_ID_PLAYER, MovementRocketHideout_B1_C)
    waitmovement(0)
    call(EventScript_RocketHideout_B1_MovePlayer)
    release
}

movement MovementRocketHideout_B1_C {
    walk_right
    walk_up
    face_right
}

script EventScript_RocketHideout_B1_MovePlayer{
    lock
    hideobjectat(13,MAP_ROCKET_HIDEOUT_B1)
    playbgm( MUS_ENCOUNTER_AQUA,TRUE)
    setflag(FLAG_MEWTWO_BEAT)
    hideobjectat(14, MAP_ROCKET_HIDEOUT_B1)
    applymovement(OBJ_EVENT_ID_PLAYER, MovementRocketHideout_B1_PlayerToGiovanni)
    waitmovement(0)
    release
    call(EventScript_RocketHideout_B1_GiovanniSequence_One)
}

movement MovementRocketHideout_B1_PlayerToGiovanni {
    emote_exclamation_mark
    walk_right * 6
    walk_up * 1
    walk_right * 1
}

script EventScript_RocketHideout_B1_GiovanniSequence_One{
    lock
    msgbox(Giovanni_RocketHideout_B1_Intro)
    waitmessage
    applymovement(10,Movement_RocketHideoutB1_Scientist_One)
    waitmovement(0)
    msgbox(format("YOU HAVE INTERFERED WITH OUR RESEARCH ENOUGH!"))
    waitmessage
    //first battle
    waitmovement(0)
    trainerbattle_single(TRAINER_GIOVANNI_ROCKETHIDEOUTB1_ONE, Giovanni_BattleOne_RocketHideout_B1_Intro, Giovanni_BattleOne_RocketHideout_B1_Outro,EventScript_RocketHideout_B1_GiovanniSequence_Two)
    releaseall
}

script EventScript_RocketHideout_B1_GiovanniSequence_Two{
    //move Scientist back
    waitmovement(0)
    applymovement(10, Movement_RocketHideoutB1_Scientist_One_Return)
    waitmovement(0)
    applymovement(12, Movement_RocketHideoutB1_Scientist_Two)
    waitmovement(0)
    //move up new scientist

    trainerbattle_single(TRAINER_GIOVANNI_ROCKETHIDEOUTB1_TWO, Giovanni_BattleTwo_RocketHideout_B1_Intro, Giovanni_BattleTwo_RocketHideout_B1_Outro,EventScript_RocketHideout_B1_GiovanniSequence_Three)
}

movement LookRightPlayer_RocketHideoutB1 {
    face_right
}

script EventScript_RocketHideout_B1_GiovanniSequence_Three{
    waitmovement(0)
    setflag(FLAG_DYNAMAX_ALLOWED)
    //move scientist back
    applymovement(12, Movement_RocketHideoutB1_Scientist_Two_Return)
    waitmovement(0)
    msgbox(format("GIOVANNI: With ease... you best my two most clever scientists. You best my gym leaders. And now you attempt to best me."))
    //move up giovanni - bring out Mewtwo
    waitmessage
    msgbox(format("GIOVANNI: MEWTWO. COME FORTH!"))
    waitmessage
    showobjectat(13,MAP_ROCKET_HIDEOUT_B1)
    msgbox(format("MEWTWO: YES. MASTER."))
    waitmessage
    applymovement(13, Movement_RocketHideout_MewtwoMoveDown)
    waitmovement(0)
    applymovement(1, Movement_RocketHideout_MoveGiovanniForward)
    waitmovement(0)
    msgbox(format("Finally we shall see. My full power, using the tools we have created, against your power"))
    waitmessage
    applymovement(1, Movement_RocketHideout_MoveGiovanniForward)
    waitmovement(0)
    trainerbattle_single(TRAINER_GIOVANNI_ROCKETHIDEOUTB1_THREE, Giovanni_BattleThree_RocketHideout_B1_Intro, Giovanni_BattleThree_RocketHideout_B1_Outro,EventScript_RocketHideout_B1_GiovanniSequence_PostBattle)
}

movement Movement_RocketHideout_MewtwoMoveDown {
    walk_down * 2
    face_down
}

movement Movement_RocketHideout_MoveGiovanniForward {
    walk_left * 1
}

script EventScript_RocketHideout_B1_GiovanniSequence_PostBattle{
    lock
    waitmovement(0)
    clearflag(FLAG_DYNAMAX_ALLOWED)
    msgbox(format("GIOVANNI: So it se-"))
    //sreenshake
    waitmessage
    msgbox(format("MEWTWO: YOU HUMANS HAVE USED POKEMON FOR THE WRONG PURPOSE. THIS CONFLICT, IT IS YOURS NOT OURS, THIS FINAL BATTLE HAS SHOWN ME... ALL HUMANS ARE HORRIBLE. I CANNOT ALLOW THIS TO CONTINUE!"))
    //flash or something
    waitmessage

    fadescreen(FADE_TO_BLACK)
    setflag(FLAG_ROCKET_EVENT_COMPLETED)
    removeobject(1)
    removeobject(8)
    removeobject(9)
    removeobject(10)
    removeobject(11)
    removeobject(12)
    removeobject(13)
    clearflag(FLAG_ROCKET_EVENT_ACTIVE)
    setflag(FLAG_MEWTWO_ISLAND_ACTIVE)
    setvar(VAR_ROCKET_ARC_STATE,9)
    clearflag(FLAG_MEWTWO_BEAT)
    setvar(VAR_MEWTWO_ARC_STATE, 1)
    clearflag(FLAG_NOTINMEWTWO_STATE_1)
    setflag(FLAG_DEFEATED_GIOVANNI)
    showobjectat(14, MAP_ROCKET_HIDEOUT_B1)
    showobjectat(15,MAP_ROCKET_HIDEOUT_B1)
    showobjectat(16,MAP_ROCKET_HIDEOUT_B1)
    msgbox(format("GIOVANNI: MEWTWO YO- ARGGHHH"))
    waitmessage
    msgbox(format("A brief moment of chaos passes and then..... Silence...."))
    waitmessage
    fadescreen(FADE_FROM_BLACK)
    release
}

text Giovanni_RocketHideout_B1_Intro{
    format("GIOVANNI: You have annoyed me {PLAYER}. We were so close to dominating this Hoenn region... but you had to interfere, hadn't you? I'm done playing nice and fair with you. You better prepare to face some of our strongest cloned Pokemons and our latest tech. My scientists, take him on!")
}

text Giovanni_RocketHideout_B1_Second{
    format("")
}

text Giovanni_RocketHideout_B1_Third{
    format("")
}

text Giovanni_BattleOne_RocketHideout_B1_Intro{
    format("I'M GOING TO END YOU!")
}
text Giovanni_BattleOne_RocketHideout_B1_Outro{
    format("No... no! NO!")
}
text Giovanni_BattleTwo_RocketHideout_B1_Intro{
    format("Bwahahaha, no rest for you!")
}
text Giovanni_BattleTwo_RocketHideout_B1_Outro{
    format("In-incredible...")
}

text Giovanni_BattleThree_RocketHideout_B1_Intro{
    format("This ends here!")
}
text Giovanni_BattleThree_RocketHideout_B1_Outro{
    format("...")
}

movement Movement_RocketHideoutB1_Scientist_One{
    walk_up * 3
    walk_left
}
movement Movement_RocketHideoutB1_Scientist_One_Return {
    walk_fast_right
    walk_fast_down * 3
    face_up
}

movement Movement_RocketHideoutB1_Scientist_Two{
    walk_left * 2
    walk_down
    walk_left * 1
}
movement Movement_RocketHideoutB1_Scientist_Two_Return {
    walk_fast_up * 3
    face_down
}

script EventScriptRocketOne_RocketHideoutB1{
    trainerbattle_single(TRAINER_ROCKETM_ROCKETHIDEOUTB1_ONE, RocketOne_RocketHideoutB1_BattleIntro, RocketOne_RocketHideoutB1_BattleOutro,EventScript_RocketOne_PostBattle_RocketHideoutB1)
    call(EventScript_RocketOne_PostBattle_RocketHideoutB1)
}

script EventScript_RocketOne_PostBattle_RocketHideoutB1{
    lock
    faceplayer
    msgbox(RocketOne_RocketHideoutB1_BattleOutro)
    waitmessage
    release
}

text RocketOne_RocketHideoutB1_BattleIntro{
    format("You got no chance here!")
}
text RocketOne_RocketHideoutB1_BattleOutro{
    format("I'm going to ask for help...")
}

script EventScriptRocketTwo_RocketHideoutB1{
    trainerbattle_single(TRAINER_ROCKETM_ROCKETHIDEOUTB1_TWO, RocketTwo_RocketHideoutB1_BattleIntro, RocketTwo_RocketHideoutB1_BattleOutro,EventScript_RocketTwo_PostBattle_RocketHideoutB1)
    call(EventScript_RocketTwo_PostBattle_RocketHideoutB1)
}

script EventScript_RocketTwo_PostBattle_RocketHideoutB1{
    lock
    faceplayer
    msgbox(RocketTwo_RocketHideoutB1_BattleOutro)
    waitmessage
    release
}

text RocketTwo_RocketHideoutB1_BattleIntro{
    format("In this lab you are the experiment!")
}
text RocketTwo_RocketHideoutB1_BattleOutro{
    format("How can I be beat by a lab rat.")
}

script EventScriptRocketThree_RocketHideoutB1{
    trainerbattle_single(TRAINER_ROCKETM_ROCKETHIDEOUTB1_THREE, RocketThree_RocketHideoutB1_BattleIntro, RocketThree_RocketHideoutB1_BattleOutro,EventScript_RocketThree_PostBattle_RocketHideoutB1)
    call(EventScript_RocketThree_PostBattle_RocketHideoutB1)
}

script EventScript_RocketThree_PostBattle_RocketHideoutB1{
    lock
    faceplayer
    msgbox(RocketThree_RocketHideoutB1_BattleOutro)
    waitmessage
    release
}

text RocketThree_RocketHideoutB1_BattleIntro{
    format("Get out my face!")
}
text RocketThree_RocketHideoutB1_BattleOutro{
    format("Darn it.")
}

script EventScriptRocketFour_RocketHideoutB1{
    trainerbattle_single(TRAINER_ROCKETM_ROCKETHIDEOUTB1_FOUR, RocketFour_RocketHideoutB1_BattleIntro, RocketFour_RocketHideoutB1_BattleOutro,EventScript_RocketFour_PostBattle_RocketHideoutB1)
    call(EventScript_RocketFour_PostBattle_RocketHideoutB1)
}

script EventScript_RocketFour_PostBattle_RocketHideoutB1{
    lock
    faceplayer
    msgbox(RocketFour_RocketHideoutB1_BattleOutro)
    waitmessage
    release
}

text RocketFour_RocketHideoutB1_BattleIntro{
    format("Do you think you have what it takes?")
}
text RocketFour_RocketHideoutB1_BattleOutro{
    format("You got me...")
}

script EventScriptRocketFive_RocketHideoutB1{
    trainerbattle_single(TRAINER_ROCKETM_ROCKETHIDEOUTB1_FIVE, RocketFive_RocketHideoutB1_BattleIntro, RocketFive_RocketHideoutB1_BattleOutro,EventScript_RocketFive_PostBattle_RocketHideoutB1)
    call(EventScript_RocketFive_PostBattle_RocketHideoutB1)
}

script EventScript_RocketFive_PostBattle_RocketHideoutB1{
    lock
    faceplayer
    msgbox(RocketFive_RocketHideoutB1_BattleOutro)
    waitmessage
    release
}

text RocketFive_RocketHideoutB1_BattleIntro{
    format("Feel like giving up?")
}
text RocketFive_RocketHideoutB1_BattleOutro{
    format("I guess not.")
}

script EventScriptRocketSix_RocketHideoutB1{
    trainerbattle_single(TRAINER_ROCKETM_ROCKETHIDEOUTB1_SIX, RocketSix_RocketHideoutB1_BattleIntro, RocketSix_RocketHideoutB1_BattleOutro,EventScript_RocketSix_PostBattle_RocketHideoutB1)
    call(EventScript_RocketSix_PostBattle_RocketHideoutB1)
}

script EventScript_RocketSix_PostBattle_RocketHideoutB1{
    lock
    faceplayer
    msgbox(RocketSix_RocketHideoutB1_BattleOutro)
    waitmessage
    setflag(FLAG_ROCKETB1_BASEMENT_GAUNTLET_BEAT)
    release
}

text RocketSix_RocketHideoutB1_BattleIntro{
    format("I'll beat you!")
}
text RocketSix_RocketHideoutB1_BattleOutro{
    format("Argh!")
}

script EventScript_MewtwoDoorBlocker{
    lock
    faceplayer
    msgbox(format("You did well. Prof Birch would like to speak with you, I would suggest looking around to see if you can find him. We will tidy up this place."))
    waitmessage
    release
}

script EventScript_MewtwoArcKickOff_RocketHideoutB1{
    lock
    applymovement(15,Movement_RocketHideout_B1_JensIntro)
    applymovement(16, Movement_RocketHideout_B1_MatsIntro)
    waitmovement(0)
    msgbox(format("JENS: Holy! That battle was epic, you are strong as steel {PLAYER}!"))
    waitmessage
    msgbox(format("MATS: No way you beat Mewtwo!"))
    waitmessage
    msgbox(format("MATS: It seems that all the rockets fled... but what happened here was something else. The Prof wants to see you {PLAYER}, he should be outside on the beach. Meet up with him when you are ready. Something is going on unfortunately we cannot stop and rest yet!"))
    waitmessage
    applymovement(15,Movement_RocketHideout_B1_JensOutro)
    applymovement(16, Movement_RocketHideout_B1_MatsOutro)
    waitmovement(0)
    setvar(VAR_MEWTWO_ARC_STATE, 2)
    setflag(FLAG_NOTINMEWTWO_STATE_1)
    removeobject(15)
    removeobject(16)
    releaseall
}

movement Movement_RocketHideout_B1_MatsIntro {
    walk_right * 6
    walk_up
    walk_right * 1
}

movement Movement_RocketHideout_B1_MatsOutro {
    walk_left * 1
    walk_down
    walk_left * 6
}

movement Movement_RocketHideout_B1_JensIntro {
    walk_right * 4
    walk_up 
    emote_exclamation_mark
    walk_fast_right * 3
}

movement Movement_RocketHideout_B1_JensOutro {
    walk_left * 3
    walk_down
    walk_left * 4
}